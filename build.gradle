buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group = 'io.izzel.lightfall'
version = '1.1.1'
archivesBaseName = 'lightfallclient'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: "1.19.3"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.debug.export', 'true'
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.dumpTargetOnFailure', 'true'
            ideaModule "${rootProject.name}.main"
            mods {
                lightfallclient {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://maven.izzel.io/releases' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.3-44.0.5'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, 'mixins.lightfall.refmap.json'
    config 'mixins.lightfall.json'
}

processResources {
    filesMatching("**/mods.toml") {
        def ver = System.getenv('GITHUB_RUN_NUMBER') ?: 'unknown'
        expand 'version': project.version + "+$ver"
    }
}

jar {
    manifest.attributes([
            "Specification-Title"     : "lightfallclient",
            //"Specification-Vendor": "mirror authors",
            "Specification-Version"   : "1", // We are version 1 of ourselves
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.jar.archiveVersion,
            //"Implementation-Vendor": "mirror authors",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}

jar.finalizedBy('reobfJar')
