buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group = 'io.izzel.lightfall'
version = '1.0.0-SNAPSHOT'
archivesBaseName = 'lightfallclient'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "20210215-1.16.3"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.debug.export', 'true'
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.dumpTargetOnFailure', 'true'
            ideaModule "${rootProject.name}.main"
            mods {
                lightfallclient {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://maven.izzel.io/releases' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.0.46'
    compile 'org.spongepowered:mixin:0.8.2'
}

mixin {
    add sourceSets.main, 'mixins.lightfall.refmap.json'
}

processResources {
    filesMatching("**/mods.toml") {
        expand 'version': project.version
    }
}

jar {
    manifest.attributes MixinConnector: 'io.izzel.lightfall.client.LightfallConnector'
}